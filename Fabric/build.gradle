plugins {
    id 'fabric-loom' version '1.6.9'
    id 'maven-publish'
    id 'idea'
}

repositories {
    maven { url "https://maven.terraformersmc.com/releases/" }
    maven { url "https://github.com/Serilum/.maven/raw/maven/" }
}

dependencies {
    minecraft "com.mojang:minecraft:1.20.1"
    mappings "net.fabricmc:yarn:1.20.1+build.10:v2"
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.83.0+1.20.1"
    modImplementation "net.fabricmc:fabric-loader:0.15.7"

    implementation "org.jetbrains:annotations:23.0.0"
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    api project(":Common")

    modApi("com.terraformersmc:modmenu:7.0.1") { exclude(module: "fabric-api") }

    // Use only the Fabric version of Collective
    modImplementation "com.natamus.collective-ml:collective-fabric:${minecraft_version}-${collective_version}"
    // Use compileOnly instead of deprecated modCompileOnly
    compileOnly "com.natamus.collective-ml:collective:${minecraft_version}-${collective_version}"
}

loom {
    mixin {
        defaultRefmapName.set("${mod_id}_fabric.refmap.json")
    }

    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}

processResources {
    from(sourceSets.main.resources) // Ensure Fabric's own resources (including fabric.mod.json) are included
    from project(":Common").sourceSets.main.resources
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

jar {
    from("LICENSE") {
        rename { "${it}_${mod_name}" }
    }
    // Include Common's compiled classes in the Fabric JAR
    from(project(":Common").sourceSets.main.output)
    // Do not override defaults; compiled classes and resources are included by default
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.name
            version project.version
            from components.java
        }
    }

    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}

archivesBaseName = 'flowframe-fabric-1.20.1'
